buildscript {
    dependencies {
        // https://mvnrepository.com/artifact/commons-io/commons-io
        classpath group: 'commons-io', name: 'commons-io', version: '2.6'

    }
}

import org.apache.commons.io.FileUtils

plugins {
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id "org.openapi.generator" version "5.0.0-beta"
}

group = 'org.mokka88'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '14'

def apiDir = "src-api"
def apiPath = "$buildDir/generated/openapi"
def basePackageName = "org.mokka88.msblueprint"
def apiPackageName = "${basePackageName}.api"

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs apiDir.toString() + "/main/java"
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    // Dependencies for generated api code
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.3.1.RELEASE'
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.8.0'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.11'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.11.1'
    compile group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.1.0'
    compile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
}

test {
    useJUnitPlatform()
}

apply plugin: 'org.openapi.generator'

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/swagger-api.yml".toString()
    outputDir = apiPath.toString()

    apiPackage = apiPackageName
    // invokerPackage = "org.mokka88.msblueprint"
    modelPackage = "${apiPackageName}.model"
    configOptions = [
            dateLibrary    : "java8",
            delegatePattern: "true",
            inerfaceOnly   : "true",
            sourceFolder   : "$apiDir/main/java"
    ]
}

task generateApi {
    dependsOn 'openApiGenerate'
    doLast {
        new File(rootDir.absolutePath, apiDir).deleteDir()
        FileUtils.moveDirectory(new File("$apiPath/$apiDir/main/java/" + apiPackageName.replace('.', '/')), new File("${rootDir.absolutePath}/$apiDir/main/java/" + apiPackageName.replace('.', '/')))
        // new File(apiPath).deleteDir()
    }
}

compileJava.dependsOn tasks.generateApi
